apply plugin: 'com.android.library'

android {
    compileSdkVersion 20
    buildToolsVersion "21.1.2"

    defaultConfig {
        // BLE is introduced at 18
        minSdkVersion 18
        targetSdkVersion 20
        versionCode 2
        versionName "1.0.1"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.2.1'
    compile 'org.altbeacon:android-beacon-library:2.1.4'
    compile files('libs/JSON4J.jar')
}

// Task to generate Javadocs
android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        title = 'Presence Insights SDK'
        description = 'Library for integrating with the Presence Insights platform'
        source = variant.javaCompile.source
        classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath())
        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
        }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}
