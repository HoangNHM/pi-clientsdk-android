apply plugin: 'com.android.library'

android {
    compileSdkVersion 17
    buildToolsVersion "22.0.1"

    defaultConfig {
        //applicationId "ibm.ae.geofencing.sdk"
        minSdkVersion 18
        targetSdkVersion 19
        versionCode 1
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            // debug and verbose logs are obfuscated away in release aar
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

dependencies {
    compile project(':pi-sdk')
    // Google Play Services location API
    compile 'com.google.android.gms:play-services-location:7.5.0'

    // Sugar ORM
    compile 'com.github.satyan:sugar:1.4'

    // unit tests dependencies
    compile 'com.android.support.test:runner:0.2'
    compile 'com.android.support.test:rules:0.2'
    compile 'com.android.support.test.espresso:espresso-core:2.1'

    // Log4j logging
    compile 'de.mindpipe.android:android-logging-log4j:1.0.3'
    compile 'log4j:log4j:1.2.17'

}

assembleRelease << {
//    copy {
//        from 'build/outputs/aar'
//        into '../dist'
//        rename { String fileName -> fileName.replace('geofencing', 'pi-client-sdk-android-geofencing') }
//        include '**/geofencing-release.aar'
//    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
    title = "IBM Adaptive Experience SDK"

    options {
        links "http://docs.oracle.com/javase/7/docs/api/"
        def androidRefPath = "${android.sdkDirectory}/docs/reference".replace("\\", "/")
        if (!androidRefPath.startsWith("/")) androidRefPath = "/" + androidRefPath
        androidRefPath = "file://" + androidRefPath
        linksOffline "http://d.android.com/reference", androidRefPath
    }
    include "**/PI*.java"
    exclude "**/PIGeofenceList.java"
    exclude "**/PIErrorCallback.java"
    exclude "**/PIHttpService.java"
    exclude "**/PIJSONPayloadRequest.java"
    exclude "**/PIRequest.java"
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
